buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

repositories {
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://tehnut.info/maven"
    }
    maven { // JEI
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven { // CompatLayer
        url "http://maven.k-4u.nl"
    }
    maven { // TOP
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
}

version = "0.0.1"
group= "com.github.mcjty"
archivesBaseName = "eve"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.11.2-13.20.0.2201"
    runDir = "run"
    mappings = "snapshot_20161227"
    useDepAts = true
}

dependencies {
    deobfCompile "mcp.mobius.waila:Hwyla:1.8.3-B15_1.11"
    deobfCompile "mezz.jei:jei_1.11.2:4.2.0.210"
    deobfCompile "mcjty.theoneprobe:TheOneProbe:1.11-1.3.4-47"
    deobfCompile "com.github.mcjty:compatlayer:1.11.2-0.1.7"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task("uploadJars", dependsOn: "reobf") {
    description = "uploads JARs"
    if (System.getenv("local_maven") != null) {
        apply plugin: 'maven'
        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: "file://" + System.getenv("local_maven"))
                    pom {
                        groupId = project.group
                        version = project.version
                        artifactId = project.archivesBaseName
                        project {
                            name project.archivesBaseName
                            packaging 'jar'
                            description 'Eve'
                            scm {
                                url 'https://github.com/McJty/Eve'
                                connection 'scm:git:git@github.com:McJty/Eve.git'
                                developerConnection 'scm:git:git@github.com:McJty/Eve.git'
                            }
                            issueManagement {
                                system 'github'
                                url 'https://github.com/McJty/Eve/issues'
                            }
                            licenses {
                                license {
                                    name 'MIT'
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'McJty'
                                    name 'McJty'
                                    roles { role 'developer' }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
